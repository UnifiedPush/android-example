import java.util.regex.Matcher
import java.util.regex.Pattern

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 34

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    defaultConfig {
        applicationId "org.unifiedpush.example"
        targetSdk 34
        versionCode 23
        versionName "1.5.5"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["version"]
    productFlavors {
        mainFlavor {
            minSdkVersion 16
            dimension "version"
        }

        fcm {
            minSdkVersion 21
            dimension "version"
            versionNameSuffix "-fcm"
        }
    }

    sourceSets {
        fcm {
            manifest.srcFile "src/fcm/AndroidManifest.xml"
            java.srcDirs = ['src/main/java', 'src/fcm/java']
        }
    }

    namespace 'org.unifiedpush.example'
}

if (project.hasProperty('sign')) {
    android {
        signingConfigs {
            release {
                storeFile file(System.getenv("RELEASE_STORE_FILE"))
                storePassword System.getenv("RELEASE_STORE_PASSWORD")
                keyAlias System.getenv("RELEASE_KEY_ALIAS")
                keyPassword System.getenv("RELEASE_KEY_PASSWORD")
            }
        }
    }
    android.buildTypes.release.signingConfig android.signingConfigs.release
}

dependencies {
    ext.connector = 'org.unifiedpush.android:connector:2.5.0'
    ext.connector_ui = "org.unifiedpush.android:connector-ui:1.0.0-rc2"
    ext.embedded_distrib = 'org.unifiedpush.android:embedded-fcm-distributor:2.5.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // Stick to 1.6.1, so minSdk an be 16
    // minsdk=21 for 1.7.0
    mainFlavorImplementation 'androidx.appcompat:appcompat:1.6.1'
    fcmImplementation 'androidx.appcompat:appcompat:1.7.0'
    // Stick to 1.11.0, so minSdk an be 16
    // minsdk=19 for 1.12
    mainFlavorImplementation 'com.google.android.material:material:1.11.0'
    fcmImplementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.crypto.tink:apps-webpush:1.10.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.multidex:multidex:2.0.1"

    //Flavors
    implementation connector  //delToDevMain//
//toDevMain//    implementation project(':connector')
    implementation connector_ui  //delToDevUILib//
//toDevUILib//    implementation project(':connector_ui')

    fcmImplementation(embedded_distrib) {  //delToDevFcm//
        exclude group: 'com.google.firebase', module: 'firebase-core'  //delToDevFcm//
        exclude group: 'com.google.firebase', module: 'firebase-analytics'  //delToDevFcm//
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'  //delToDevFcm//
    }  //delToDevFcm//
//toDevFcm//    fcmImplementation  project(':distributor')
}

def getCurrentFlavor() {
    Gradle gradle = getGradle()
    String  tskReqStr = gradle.getStartParameter().getTaskRequests().toString()
    String flavor

    Pattern pattern

    if( tskReqStr.contains( "assemble" ) )
        pattern = Pattern.compile("assemble(\\w+)")
    else
        pattern = Pattern.compile("generate(\\w+)")

    Matcher matcher = pattern.matcher( tskReqStr )

    if( matcher.find() ) {
        flavor = matcher.group(1).toLowerCase()
    }
    else
    {
        println "NO MATCH FOUND"
        return ""
    }

    pattern = Pattern.compile("^fcm.*");
    matcher = pattern.matcher(flavor);

    if( matcher.matches() ) {
        return "fcm"
    } else {
        return "main"
    }
}

println("Flavor: ${getCurrentFlavor()}")
if ( getCurrentFlavor() == "fcm" ){
    apply plugin: 'com.google.gms.google-services'
}
